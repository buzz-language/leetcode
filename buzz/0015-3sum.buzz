import "std";

// Given an integer array nums, return all the triplets [nums[i], nums[j], nums[k]] such that
// i != j, i != k, and j != k, and nums[i] + nums[j] + nums[k] == 0.
// Notice that the solution set must not contain duplicate triplets.

fun threeSum(nums: [int]) > [[int]] {
    final result = mut [ [<int>] ];
    final sorted = nums.cloneMutable().sort(fun (a: int, b: int) => a < b);

    foreach (i, a in sorted) {
        if (i > 0 and a == sorted[i - 1]) {
            continue;
        }

        var l = i + 1;
        var r = sorted.len() - 1;
        while (l < r) {
            final sum = a + sorted[l] + sorted[r];

            if (sum < 0) {
                l += 1;
            } else if (sum > 0) {
                r -= 1;
            } else {
                result.append([ a, sorted[l], sorted[r] ]);
                l += 1;
                r -= 1;

                while (l < r and sorted[l] == sorted[l - 1]) {
                    l += 1;
                }
            }
        }
    }

    return result;
}

test "3Sum" {
    // Input: nums = [-1,0,1,2,-1,-4]
    // Output: [[-1,-1,2],[-1,0,1]]
    // Explanation: 
    // nums[0] + nums[1] + nums[2] = (-1) + 0 + 1 = 0.
    // nums[1] + nums[2] + nums[4] = 0 + 1 + (-1) = 0.
    // nums[0] + nums[3] + nums[4] = (-1) + 2 + (-1) = 0.
    // The distinct triplets are [-1,0,1] and [-1,-1,2].
    // Notice that the order of the output and the order of the triplets does not matter.
    var result = threeSum([ -1, 0, 1, 2, -1, -4 ]);
    std\assert(
        result.len() == 2
        and result[0][0] == -1
        and result[0][1] == -1
        and result[0][2] == 2
        and result[1][0] == -1
        and result[1][1] == 0
        and result[1][2] == 1,
    );

    // Input: nums = [0,1,1]
    // Output: []
    // Explanation: The only possible triplet does not sum up to 0.
    std\assert(
        threeSum([ 0, 1, 1 ]).len() == 0
    );

    // Input: nums = [0,0,0]
    // Output: [[0,0,0]]
    // Explanation: The only possible triplet sums up to 0.
    result = threeSum([ 0, 0, 0 ]);
    std\assert(
        result.len() == 1
        and result[0][0] == 0
        and result[0][1] == 0
        and result[0][2] == 0
    );
}
