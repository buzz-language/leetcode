import "std";

// You are given two non-empty linked lists representing two non-negative integers. The digits are stored in reverse order,
// and each of their nodes contains a single digit. Add the two numbers and return the sum as a linked list.
// You may assume the two numbers do not contain any leading zero, except the number 0 itself.

object ListNode {
    val: int = 0,
    next: ListNode? = null
}

fun addTwoNumbers(l1: ListNode, l2: ListNode) > ListNode? {
    var listNode: ListNode? = null;
    var carry = 0;
    var list1: ListNode? = l1;
    var list2: ListNode? = l2;
    while (list1 != null or list2 != null) {
        var val = (list1?.val ?? 0) + (list2?.val ?? 0) + carry;
        list1 = list1?.next;
        list2 = list2?.next;

        if (val > 9) {
            val -= 10;
            carry = 1;
        } else {
            carry = 0;
        }

        if (listNode != null) {
            listNode = ListNode{
                val,
                next = listNode,
            };
        } else {
            listNode = ListNode{
                val
            };
        }
    }

    if (carry != 0) {
        listNode = ListNode{
            val = carry,
            next = listNode,
        };
    }

    var result: ListNode? = null;
    while (listNode != null) {
        result = ListNode{
            val = listNode!.val,
            next = result,
        };
        listNode = listNode?.next;
    }

    return result;
}

test "add two numbers" {
    // Input: l1 = [2,4,3], l2 = [5,6,4]
    // Output: [7,0,8]
    // Explanation: 342 + 465 = 807.
    var result = addTwoNumbers(
        l1: ListNode{
            val = 2,
            next = ListNode {
                val = 4,
                next = ListNode {
                    val = 3,
                }
            },
        },
        l2: ListNode{
            val = 5,
            next = ListNode {
                val = 6,
                next = ListNode {
                    val = 4,
                }
            },
        },
    );

    std\assert(
        result != null and result?.val == 7 and
        result?.next?.val == 0 and
        result?.next?.next?.val == 8
    );

    // Input: l1 = [0], l2 = [0]
    // Output: [0]
    result = addTwoNumbers(
        l1: ListNode{ val = 0 },
        l2: ListNode{ val = 0 },
    );

    std\assert(
        result != null and result?.val == 0
    );

    // Input: l1 = [9,9,9,9,9,9,9], l2 = [9,9,9,9]
    // Output: [8,9,9,9,0,0,0,1]
    result = addTwoNumbers(
        l1: ListNode{
            val = 9,
            next = ListNode {
                val = 9,
                next = ListNode {
                    val = 9,
                    next = ListNode {
                        val = 9,
                        next = ListNode {
                            val = 9,
                            next = ListNode {
                                val = 9,
                                next = ListNode {
                                    val = 9,
                                }
                            }
                        }
                    }
                }
            },
        },
        l2: ListNode{
            val = 9,
            next = ListNode {
                val = 9,
                next = ListNode {
                    val = 9,
                    next = ListNode {
                        val = 9,
                    }
                }
            }
        },
    );

    std\assert(
        result != null and
        result?.val == 8 and
        result?.next?.val == 9 and
        result?.next?.next?.val == 9 and
        result?.next?.next?.next?.val == 9 and
        result?.next?.next?.next?.next?.val == 0 and
        result?.next?.next?.next?.next?.next?.val == 0 and
        result?.next?.next?.next?.next?.next?.next?.val == 0 and
        result?.next?.next?.next?.next?.next?.next?.next?.val == 1
    );
}
